IEFU84 TITLE 'WSRCC MODIFIED SMF RECORD EXIT'                           00002
         PRINT GEN                                                      00003
*/* REFER:  IPO1.SAMPLIB(IEFU84)                                        00004
*/* COMPID: SYSTEM                                                      00005
*/* DOC     THIS IS A SAMPLE SMF EXIT ROUTINE TO SUPRESS WRITING        00006
*/*         SELECTED SMF RECORD TYPES.                                  00007
*/*                                                                     00007
*/* NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE    00007
*/*                                                                     00007
*/* THIS MODULE IS SIMPLY LINKEDITTED INTO LPALIB RENT.  FOR TESTING    00007
*/* VIA MLPA, MAKE CERTAIN THAT IT IS NOT IN IEAFIX##.                  00007
*/*                                                                     00007
*/* NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE    00007
*/*                                                                     00007
*********************************************************************** 00008
*                                                                     * 00009
*             MODULE NAME = IEFU84                                    * 00010
*                                                                     * 00011
*             DESCRIPTIVE NAME = WSRCC MODIFIED SMF RECORD EXIT.      * 00012
*                                                                     * 00013
*             COPYRIGHT = NONE                                        * 00014
*                                                                     * 00015
*             FUNCTION =                                              * 00016
*             WSRCC HAS ALSO ADDED SUPPORT FOR A USER CVT (WCVT).     * 00029
*             AN AREA IS GETMAINED WHEN U84 IS ENTERED                * 00030
*             FOR THE TYPE 0 IPL SMF RECORD.  THIS AREA IS IN THE     * 00031
*             SYSTEM SUBPOOL 245 - FIXED, NOT FETCH PROTECETED,       * 00032
*             AND STORE PROTECTED.                                    * 00033
*             MODIFICATIONS DONE BY TOM FOTH, JOE MARTIN              * 00034
*             AND JAN HELD.                                           * 00035
*             W.S.R.C.C.   03/31/81  SYSTEMS SOFTWARE UNIT.           * 00036
*                                                                     * 00037
*             DEPENDENCIES = NONE                                     * 00048
*                                                                     * 00049
*             RESTRICTIONS = NONE                                     * 00050
*                                                                     * 00051
*                                                                     * 00060
*             MODULE TYPE = EXECUTABLE                                  00061
*                                                                     * 00062
*                PROCESSOR = ASM                                      * 00063
*                                                                     * 00064
*                ATTRIBUTES = PROTECTION KEY 0, REENTRANT, ENABLED    * 00067
*                                                                     * 00068
*                                                                     * 00069
*             ENTRY POINTS = IEFU84 (ONLY ENTRY POINT)                * 00070
*                                                                     * 00071
*             INPUT = REGISTER 1 POINTS TO A 4 BYTE ADDRESS OF THE    * 00072
*                RECORD DESCRIPTOR WORD (RDW) OF THE SMF RECORD TO    * 00073
*                BE PROCESSED BY THE EXIT.                            * 00074
*                                                                     * 00075
*             OUTPUT= REGISTER 15 MUST CONTAIN ONE OF THE FOLLOWING:  * 00076
*                0 - WRITE THE SMF RECORD (KEEP)                      * 00077
*                4 - DO NOT WRITE THE SMF RECORD (DELETE)             * 00078
*                                                                     * 00079
*             EXTERNAL REFERENCES = NONE                              * 00080
*                                                                     * 00081
*             EXITS - NORMAL = AT PROGRAM END VIA BRANCH REGISTER 14  * 00082
*                OUTPUT = NONE                                        * 00083
*                RETURN CODE = SEE FUNCTION ABOVE                     * 00084
*                                                                     * 00085
*             EXITS - ERROR = NONE                                    * 00086
*                OUTPUT = NONE                                        * 00087
*                RETURN CODE = ZERO                                   * 00088
*                                                                     * 00089
*             CHANGE ACTIVITY = NONE                                  * 00094
*                                                                     * 00095
*             CHARACTER CODE DEPENDENCY = NONE                        * 00096
*                                                                     * 00097
*             NOTES = MACROS USED                                     * 00098
*                                                                     * 00099
*             ABEND CODES = NONE                                      * 00100
*                                                                     * 00101
*********************************************************************** 00102
         EJECT                                                          00103
*--------------MODIFICATION.LOG---------------------------------------* 00104
*  PROGRAMMER  |  DATE  |  SUMMARY OF CHANGES MADE TO THIS PROGRAM    * 00106
*--------------+--------+---------------------------------------------* 00107
* JOE MARTIN   |02/01/81| #1. ADDED USERCVT GETMAIN AND STORE OF      * 00108
*              |        |     ADDRESS INTO CVTUSER ON ENTRY WITH      * 00109
*              |        |     RECORD TYPE "0".                        * 00110
* JAN HELD     |10/21/81| #2. ADDED CHECK FOR PREVIOUS STORE OF       * 00111
*              |        |     USERCVT ADDRESS INTO CVTUSER PRIOR TO   * 00112
*              |        |     GETMAIN AND STORE DESCRIBED IN #1.      * 00113
* JAN HELD     |10/22/82| #3. MODIFIED THE PROGRAM TO BE ABLE TO      * 00114
*              |        |     RUN IN LOCKED OR SRB MODE.              * 00115
* JOE MARTIN   |11/17/81| #4. REMOVED TIME SVC FROM CODE - IT CAUSED  * 00116
*              |        |     SMF TO ABEND BECAUSE OF BEING IN LOCKED * 00117
*              |        |     MODE ON ENTRY.  USING THE DATE AND TIME * 00118
*              |        |     FROM THE TYPE 0 RECORD INSTEAD.         * 00119
* HELD, MARTIN,|  12/81 | #5. MISC. CHANGES TO ALLOW EXIT TO FUNCTION * 00119
* FOTH         |        |     UNDER MVS/SP1 @ 8107.  INCLUDED IS      * 00120
*              |        |     CONDITIONALLY GETTING THE SALLOC AND    * 00121
*              |        |     LOCAL LOCK IF AN IPL SMF RECORD (TYPE 0)* 00122
*              |        |     IS PROCESSED AND A GETMAIN FOR THE LOCAL* 00123
*              |        |     CVT IS NECESSARY                        * 00124
* TOM FOTH     |09/15/82| #6. GENERAL CLEANUP AND ENHANCEMENT.  MOST  * 00125
*              |        |     DISPLACEMENTS ARE NOW RESOLVED VIA      * 00126
*              |        |     LABELS IN MAPPING DSECTS.  FURTHER, THE * 00127
*              |        |     BINARY TIME OF DAY IS CONVERTED TO UN-  * 00128
*              |        |     SIGNED PACKED (HHMMSSTH).  FINALLY, NEW * 00129
*              |        |     FIELDS (WCVTTLEN, WCVTMLEN, WCVTULEN)   * 00130
*              |        |     ARE IMPLEMENTED (FOR TOTAL LENGTH,      * 00131
*              |        |     MAPPED LENGTH, AND USED LENGTH.         * 00132
*              |        |                                             * 00133
* JOE MARTIN   |01/03/84| #7. ADDED SUPPORT FOR WCVT LAST MODIFIED    * 00134
*              |        |     TIME AND DATE.                          * 00135
*              |        |                                             * 00136
*              |        |                                             * 00152
*---------------------------------------------------------------------* 00153
         EJECT                                                          00154
IEFU84   CSECT                                                          00155
R00      EQU   0                   USED BY SETLOCK FOR SAVING REGS       00156
R0       EQU   0                   USED BY SETLOCK FOR SAVING REGS       00156
R01      EQU   1                   WORK REGISTER-AT ENTRY ADDR SMF REC   00157
R1       EQU   1                   WORK REGISTER-AT ENTRY ADDR SMF REC   00157
R02      EQU   2                   WORK REGISTER-CLR, TIME RTN           00158
R2       EQU   2                   WORK REGISTER-CLR, TIME RTN           00158
R03      EQU   3                   WORK REGISTER-CLR, TIME RTN           00159
R3       EQU   3                   WORK REGISTER-CLR, TIME RTN           00159
R04      EQU   4                   WORK REGISTER-CLR, TIME RTN           00160
R4       EQU   4                   WORK REGISTER-CLR, TIME RTN           00160
R05      EQU   5                   WORK REGISTER-CLR, TIME RTN           00163
R5       EQU   5                   WORK REGISTER-CLR, TIME RTN           00163
R06      EQU   6                   WORK REGISTER-TIME RTN                00162
R6       EQU   6                   WORK REGISTER-TIME RTN                00162
R07      EQU   7                   GETS ADDRESS OF THE CVT               00161
R7       EQU   7                   GETS ADDRESS OF THE CVT               00161
R08      EQU   8                   POINTS TO USER CVT (WCVT)             00164
R8       EQU   8                   POINTS TO USER CVT (WCVT)             00164
R09      EQU   9                   BASE REGISTER                         00165
R9       EQU   9                   BASE REGISTER                         00165
R10      EQU   10                  POINTER TO THE SMF RECORD             00166
R11      EQU   11                  LOCKS OBTAINED REGISTER               00167
R12      EQU   12                  NOT USED                             00168
R13      EQU   13                  SAVEAREA REG                         00169
R14      EQU   14                  RETURN ADDRESS                       00170
R15      EQU   15                  ENTRY ADDRESS & TEMPORARY BASE       00171
         USING *,R15               TEMPORARY BASE FOR                   00187
         B     START               BRANCHING AROUND                     00188
         DC    CL8'IEFU84  '       MODULE IDENTIFIER                    00189
         DC    CL8'*WSRCC**'
         DC    CL8'&SYSDATE'
         DC    CL8' @ &SYSTIME'
START    EQU   *                                                        00190
         STM   R14,R12,12(R13)     SAVE CALLERS REGISTERS               00191
         LR    R09,R15             SET UP ADDRSSABILITY IN R09
         DROP  R15                 DROP R15 FOR ADDRESSABILITY
         USING IEFU84,R09
*
*  CHECK FOR TYPE 0 SMF RECORD
*
         L     R10,0(0,R01)        GET ADDRESS OF SMF RECORD
         USING SMFRCD0,R10
         XR    R03,R03             CLEAR WORKREG
         ICM   R03,X'01',SMF0RTY   GET SMF RECORD TYPE FROM RECORD
         BNZ   RESTREGS            IF NOT TYPE 0, GO AWAY
*
*   CHECK USER CVT FOR ZEROES
*
         L     R07,CVTPTR          POINT AT CVT
         USING CVT,R07             ESTAB. ADDR. FOR CVT
         L     R03,CVTUSER         GET CVTUSER FOR CHECK
         LTR   R03,R03             CHECK FOR PREVIOUS WCVT USER
         BNZ   RESTREGS            IF NOT ZEROES, GO AWAY
*
*   OBTAIN GLOBAL LOCK
*
LOCKUP   SETLOCK OBTAIN,TYPE=SALLOC,MODE=UNCOND,REGS=USE,              +
               RELATED=(UNLOCK,'GET SALLOC')                        #5.
         C     R15,=F'8'           WAS RETURN CODE 8
         BNE   LOCKOK              NO LOCK WAS OKAY
         SDUMP HDR='WSRCC ERROR, SETLOCK FAILED IN IEFU84',BRANCH=YES, +
               ASID=0
         B     RESTREGS            RETURN WITH NO USER CVT
LOCKOK   LR    R11,R15             SAVE RETURN CODE IN R11
*
*   GET SPACE FOR USERCVT AND FILL WITH GOODIES
*
         GETMAIN RC,LV=WCVTGMSZ,SP=245,BRANCH=(YES,GLOBAL),BNDRY=PAGE
         C     R15,=F'8'
         BNE   GOTMAIN
         SDUMP HDR='WSRCC ERROR, GETMAIN FAILED IN IEFU84',BRANCH=YES, +
               ASID=0
         B     RLSELOCK
GOTMAIN  LR    R08,R01             R08 IS ADDRESS OF WCVT
         USING WCVT,R08            MAP IT
         ST    R08,CVTUSER         STORE WCVT ADDRESS IN CVTUSER
         LR    R02,R08             POINT TO THE WCVT FOR CLEARING
         LA    R03,WCVTGMSZ        LENGTH TO CLEAR
         SR    R04,R04             SOURCE IS INCONSEQUENTIAL
         SR    R05,R05             LENGTH IS ZERO
         MVCL  R02,R04             CLEAR THE AREA
         MVC   WCVTID,WSRCCCVT     MOVE LITERAL INTO WCVT
         MVC   WCVTDATE,SMF0DTE    MOVE IN DATE
         MVC   WCVTSID,SMF0SID     MOVE IN THE SYSTEM ID
         LA    R02,WCVTLEN         GET LENGTH OF MAPPED CVT
         ST    R02,WCVTMLEN        SAVE IT (MAPPED LENGTH)
         ST    R02,WCVTULEN        SAVE IT (USED LENGTH)
         LA    R02,WCVTGMSZ        GET LENGTH OF GETMAIN
         ST    R02,WCVTTLEN        SAVE IT
*
* FOLLOWING CODE CONVERTS BINARY HUNDREDTHS OF SECONDS TO
* UNSIGNED PACKED HHMMSSTH FORMAT.
*
         ICM    R04,B'1111',SMF0TME GET THE TIME
         SRDA   R04,32         MOVE TO R5 W/SIGN EXTENSION
         D      R04,=F'360000' DIVIDE BY 100THS OF MINUTES
         LR     R06,R04        SAVE THE REMAINDER
         M      R04,=F'10000000' SHIFT LEFT
         LR     R02,R05        SAVE IT
         LR     R04,R06        RESTORE REMIANDER TO R4
         SRDA   R04,32         MOVE TO R5 W/SIGN EXTENSION
         D      R04,=F'6000'   DIVIDE BY 100THS SECONDS
         LR     R06,R04        SAVE THE REMAINDER
         M      R04,=F'100000' SHIFT LEFT
         AR     R02,R05        ADD TO RUNNING SUM
         LR     R05,R06        RESTORE THE REMAINDER
         M      R04,=F'10'     SHIFT LEFT
         AR     R02,R05        ADD TO RUNNING SUM
         CVD    R02,WCVTRS01   SAVE TIME
*
* WCVTRS01+3  NOW CONTAINS PACKED UNSIGNED TIME
*
         MVC   WCVTTIME,WCVTRS01+3 MOVE PASCKED TIME TO CVT
         XC    WCVTRS01(8),WCVTRS01 CLEAR TEMP STORAGE
         MVC   WCVTMDTM(8),WCVTTIME MOVE IPL TIME/DATE TO MOD T/D   #7.
*
*   RELEASE LOCAL LOCK, IF NECESSARY, RESTORE REGS, AND RETURN
*
RLSELOCK LTR   R11,R11             Q. WERE ANY LOCKS GOTTEN
         BNZ   RESTREGS            A. NO, NONE TO RELEASE
UNLOCK   SETLOCK RELEASE,TYPE=SALLOC,REGS=USE,                         +
               RELATED=(LOCKUP,'RLSE LOCK')                         #5.
RESTREGS L     R14,12(R13)         RESTORE RETURN ADDRESS               00245
         SR    R15,R15             CLEAR RETURN CODE (ALWAYS RET ZERO)  00246
         LM    R00,R12,20(R13)     RESTORE REGS ZERO THRU 12            00247
         BR    R14                 RETURN                               00248
WSRCCCVT DC    CL8'WSRCCCVT'       LITERAL FOR USER CVT (WCVT)
         PRINT NOGEN
         WSRCCCVT
         CVT   PREFIX=NO,DSECT=YES                                      00293
         IHAPSA DSECT=YES
         IHAWSAVT DSECT=YES,CLASS=GLOBAL
SMFDSCT  DSECT
         IFASMFR 0
         END                                                            00294
