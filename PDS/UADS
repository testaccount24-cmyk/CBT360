*---------------------------------------------------------------------  00001
*                                                                       00002
*        THIS TSO COMMAND PROCESSOR WILL ALLOW THOSE USERS WITH         00003
*        OPERATOR ABILITY TO DYNAMICALLY CHANGE THEIR UADS ENTRIES      00004
*        IN THE PSCB.  ONCE CHANGED, IT WILL REMAIN IN EFFECT           00005
*        ACCROSS A RELOGON (UNLESS USING ACF2 IN NOUADS MODE).          00006
*                                                                       00007
*        THOSE ENTRIES THAT MAY BE CHANGED AND THEIR ASSOCIATED         00008
*        KEYWORDS ARE LISTED BELOW:                                     00009
*                                                                       00010
*        ID(????)/RESET    CHANGE CURRENT USER-ID TO ???? OR RESET      00011
*                          BACK TO REAL ID. THIS OPERAND IS ALLOWED     00012
*                          ONLY FOR USERS WITH ACCOUNT AUTHORITY,       00013
*                          SINCE ONE MUST INSURE THAT PRIOR TO          00014
*                          LOGOFF THE NEW ID MUST BE RESET.  IF NOT     00015
*                          RESET, THE NEW ID SET WILL BE UPDATED        00016
*                          WITH THE UADS ENTRY FOR THE ACTIVE USER.     00017
*        LISTID/LID        LIST THE CURRENT UADS ID FROM PSCB.
*                                                                       00018
*        DEST(????)/NODEST CHANGE THE CURRENT DEFAULT DESTINATION       00019
*                          TO ???? OR NULLIFY IT.                       00020
*        LISTDEST/LDEST    LIST CURRENT DEFAULT DESTINATION.
*                                                                       00021
*        UNIT(????)/NOUNIT CHANGE THE CURRENT DEFAULT DYNAMIC           00022
*                          ALLOCATION UNITNAME TO ???? OR NULLIFY IT.   00023
*        LISTUNIT/LUNIT    LIST CURRENT DEFAULT UNIT ASSIGNMENT.
*                                                                       00024
*        JCL/NOJCL         ALLOW OR DISALLOW BATCH JOB SUBMISSIONS.     00025
*                                                                       00026
*        MOUNT/NOMOUNT     ALLOW OR DISALLOW FOREGROUND TAPE/DISK       00027
*                          MOUNT CAPABILITIES.                          00028
*                                                                       00029
*        PASS/NOPASS       ALLOW OR DISALLOW DATASET PASSWORD CHECKING  00030
*                                                                       00031
*---------------------------------------------------------------------  00032
         SPACE                                                          00033
*--------------MODIFICATION.LOG---------------------------------------*
*                                                                     *
*  PROGRAMMER  |  DATE  |  SUMMARY OF CHANGES MADE TO THIS PROGRAM    *
*--------------+--------+---------------------------------------------*
* J. MARTIN    |01/05/82| ADDED LIST KEYWORDS TO "ID" "DEST" AND      *
*              |        | "UNIT" FIELDS.                              *
*              |        |                                             *
*---------------------------------------------------------------------*
         EJECT
         SPACE                                                          00033
         PRINT ON,NOGEN                                                 00034
         SPACE                                                          00035
UADS     BEGIN                                                          00036
         SPACE                                                          00037
*        REGISTER USAGE:                                                00038
*                                                                       00039
*        R0-R8 =====> WORK REGISTERS                                    00040
*                                                                       00041
*        R9    =====> BASE FOR THE PSCB                                 00042
*        R10   =====> BASE FOR THE PARSE PDE'S                          00043
*        R11   =====> BASE FOR THE CPPL                                 00044
*        R12   =====> BASE FOR UADS (THIS ROUTINE)                      00045
         EJECT                                                          00046
*---------------------------------------------------------------------* 00047
*                                                                       00048
*        RETRIEVE INITIALIZATION PARMS                                  00049
*        LINK TO PARSE TO RETRIEVE COMMAND DATA                         00050
*                                                                       00051
*---------------------------------------------------------------------* 00052
         SPACE                                                          00053
ENTRY010 DS    0H                                                       00054
         LR    R11,R1             SAVE CPPL POINTER                     00055
         USING CPPL,R11           PROVIDE ADDRESSABILITY                00056
         MVC   PPLUPT,CPPLUPT     COPY UPT ADDRESS                      00057
         MVC   PPLECT,CPPLECT     COPY ECT ADDRESS                      00058
         MVC   PPLCBUF,CPPLCBUF   COPY ADDRESS OF COMMAND BUF           00059
         L     R9,CPPLPSCB        ADDR OF PSCB                          00060
         USING PSCB,R9                                                  00061
         LA    R1,PPLUPT          PROVIDE LIST FOR PARSE                00062
         LINK  EP=IKJPARS         PARSE COMMAND                         00063
         AUTH  ON                 GET  AUTHORIZATION                    00064
         MODESET KEY=ZERO                                               00065
         L     R10,PPLANSR        GET POINTER TO ANSWER AREA            00066
         USING IKJPARMD,R10       PROVIDE ADDRESSABILITY                00067
         SPACE                                                          00068
EXIT010  EQU   *                                                        00069
         B     ENTRY020                GO PROCESS USER-ID OPERANDS      00070
         EJECT                                                          00071
*---------------------------------------------------------------------* 00072
*                                                                       00073
*        PROCESS USER-ID OPERANDS                                       00074
*                                                                       00075
*---------------------------------------------------------------------* 00076
         SPACE                                                          00077
ENTRY020 DS    0H                                                       00078
         LH    R15,USERID         GET NUMBER OF SPECIFIED KEYWORD       00079
         SLL   R15,2              MULTIPLY BY FOUR                      00080
         B     *+4(R15)           TO PROCESSING ROUTINE                 00081
         B     EXIT020            NOT SPECIFIED, SKIP IT                00082
         B     BBB020             RESET SPECIFIED, GO TO IT             00083
         B     AAA020             PROCESS NEW USERID                    00084
         B     EEE020             PROCESS LISTID                        00085
         B     EEE020             PROCESS LISTID                        00086
         SPACE                                                          00087
AAA020   EQU   *                                                        00088
         L     R1,UID             ADDRESS OF NEW USERID                 00089
         LH    R3,UID+4           GET SIZE OF ID                        00090
         B     DDD020             TO COMMON ROUTINE                     00091
         SPACE                                                          00092
BBB020   EQU   *                                                        00093
         L     R3,16              ADDR OF CVT                           00094
         L     R3,0(R3)           ADDR OF OLD/NEW PTRS                  00095
         L     R3,12(R3)          ADDR OF CURRENT ASCB                  00096
         L     R1,176(R3)         ADDR OF REAL USER-ID                  00097
         LR    R3,R1                                                    00098
         MVC   PSCBUSER,BLANKS                                          00099
         LA    R15,7              MAX SIZE OF USERID                    00100
         SPACE                                                          00101
LOOP020  EQU   *                                                        00102
         CLI   0(R3),C' '         END OF ID ?                           00103
         BE    CCC020             YES - OK                              00104
         CLI   0(R3),X'00'        END OF ID ?                           00105
         BE    CCC020             YES - OK                              00106
         LA    R3,1(R3)           BUMP                                  00107
         BCT   R15,LOOP020        CHECK ALL BYTES                       00108
         SPACE                                                          00109
CCC020   EQU   *                                                        00110
         SR    R3,R1              LENGTH OF USERID                      00111
         SPACE                                                          00112
DDD020   EQU   *                                                        00113
         TM    PSCBATR1,PSCBACCT  DOES USER HAVE ACCOUNT AUTHORITY?     00114
         BZ    ERR020             NO, INFORM USER                       00115
         STC   R3,PSCBUSRL        STORE LENGTH                          00116
         BCTR  R3,R0              DECR FOR EX                           00117
         EX    R3,MVCID1          MOVE NEW USERID INTO PSCB             00118
         B     EXIT020            EXIT FROM USER-ID ROUTINE             00119
EEE020   EQU   *                                                        00120
         MVC   MSG0202+20(8),BLANKS    CLEAR THE USERID FIELD           00121
         SR    R3,R3                   CLEAR LENGTH OF OLD USERID       00122
         IC    R3,PSCBUSRL             GET LENGTH OF USERID FIELD       00123
         BCTR  R3,0                    REDUCE FOR EX MVC                00124
         EX    R3,MVCID2               MOVE IN USERID TO MESSAGE        00125
         TPUT  MSG0202,60              MESSAGE TO USER                  00126
         B     EXIT020                 EXIT FROM LIST USER-ID RTN       00127
         SPACE                                                          00128
ERR020   EQU   *                                                        00129
         TPUT  MSG0201,60              ISSUE ERROR MESSAGE              00130
         B     EXIT020                 EXIT FROM USER-ID ROUTINE        00131
         SPACE                                                          00132
EXIT020  EQU   *                                                        00133
         B     ENTRY030                GO DO NEXT CHECK                 00134
         SPACE                                                          00135
MVCID1   MVC   PSCBUSER(0),0(R1)                                        00136
MVCID2   MVC   MSG0202+20(0),PSCBUSER                                   00137
MSG0201  DC    CL60'USER-ID CHANGE NOT AUTHORIZED'                      00138
MSG0202  DC    CL60'CURRENT USERID IS -          '                      00139
         EJECT                                                          00140
*---------------------------------------------------------------------* 00141
*                                                                       00142
*        PROCESS UNIT OPERANDS                                          00143
*                                                                       00144
*---------------------------------------------------------------------* 00145
         SPACE                                                          00146
ENTRY030 DS    0H                                                       00147
         LH    R15,UNIT           GET NUMBER OF SPECIFIED KEYWORD       00148
         SLL   R15,2              MULTIPLY BY FOUR                      00149
         B     *+4(R15)           TO PROCESSING ROUTINE                 00150
         B     EXIT030            NOT SPECIFIED, SKIP IT                00151
         B     BBB030             NOUNIT SPECIFIED, GO TO IT            00152
         B     AAA030             UNIT SPECIFIED -- GO FOR IT           00153
         B     CCC030             LISTUNIT SPECIFIED                    00154
         B     CCC030             LUNIT SPECIFIED                       00155
         SPACE                                                          00156
AAA030   EQU   *                                                        00157
         L     R1,UNITN           UNIT SPECIFIED, DO IT                 00158
         LH    R2,UNITN+4         GET SIZE OF NAME                      00159
         BCTR  R2,R0              DECR FOR EX                           00160
         EX    R2,MVCUNIT         MOVE UNIT                             00161
         B     EXIT030                 EXIT FROM UNIT                   00162
         SPACE                                                          00163
BBB030   EQU   *                                                        00164
         MVC   PSCBGPNM,BLANKS         CLEAR OUT UNIT NAME              00165
         B     EXIT030                 EXIT FROM NOUNIT                 00166
         SPACE                                                          00167
CCC030   EQU   *                                                        00168
         MVC   MSG0301+29(8),PSCBGPNM  MOVEIN THE UNIT                  00169
         TPUT  MSG0301,60              ISSUE MESSAGE TO USER            00170
         B     EXIT030                 EXIT FROM LISTUNIT               00171
         SPACE                                                          00172
EXIT030  DS    0H                                                       00173
         B     ENTRY040                GO CHECK DEST OPERANDS           00174
         SPACE                                                          00175
MVCUNIT  MVC   PSCBGPNM(0),0(R1)                                        00176
MSG0301  DC    CL60'CURRENT ALLOCATION UNIT IS -          '             00177
         EJECT                                                          00178
*---------------------------------------------------------------------* 00179
*                                                                       00180
*        PROCESS DEST OPERANDS                                          00181
*                                                                       00182
*---------------------------------------------------------------------* 00183
ENTRY040 DS    0H                                                       00184
         LH    R15,DEST           GET NUMBER OF SPECIFIED KEYWORD       00185
         SLL   R15,2              MULTIPLY BY FOUR                      00186
         B     *+4(R15)           TO PROCESSING ROUTINE                 00187
         B     EXIT040            NOT SPECIFIED, SKIP IT                00188
         B     BBB040             NODEST SPECIFIED, GO TO IT            00189
         B     AAA040             DEST SPECIFIED -- GO FOR IT           00190
         B     CCC040             LISTDEST SPECIFIED                    00191
         B     CCC040             LDEST SPECIFIED                       00192
         SPACE                                                          00193
AAA040   EQU   *                                                        00194
         MVC   PSCBDEST,BLANKS                                          00195
         L     R1,DESTN           DEST SPECIFIED, DO IT                 00196
         LH    R2,DESTN+4         GET SIZE OF DEST NAME                 00197
         BCTR  R2,R0              DECR FOR EX                           00198
         EX    R2,MVCDEST         MOVE DEST                             00199
         B     EXIT040                                                  00200
         SPACE                                                          00201
BBB040   EQU   *                                                        00202
         MVC   PSCBDEST,BLANKS                                          00203
         B     EXIT040                 EXIT FROM LIST                   00204
         SPACE                                                          00205
CCC040   EQU   *                                                        00206
         MVC   MSG0401+32(8),PSCBDEST  MOVE IN DESTINATION              00207
         TPUT  MSG0401,60              ISSUE MESSAGE TO USER            00208
         B     EXIT040                 EXIT FROM LIST                   00209
         SPACE                                                          00210
EXIT040  DS    0H                                                       00211
         B     ENTRY050                                                 00212
         SPACE                                                          00213
MVCDEST  MVC   PSCBDEST(0),0(R1)                                        00214
MSG0401  DC    CL60'CURRENT OUTPUT DESTINATION IS -          '          00215
         EJECT                                                          00216
*---------------------------------------------------------------------* 00217
*                                                                       00218
*        PROCESS JCL OPERANDS                                           00219
*                                                                       00220
*---------------------------------------------------------------------* 00221
         SPACE                                                          00222
ENTRY050 DS    0H                                                       00223
         LH    R15,JCL            GET NUMBER OF SPECIFIED KEYWORD       00224
         SLL   R15,2              MULTIPLY BY FOUR                      00225
         B     *+4(R15)           TO PROCESSING ROUTINE                 00226
         B     EXIT050            NOT SPECIFIED, SKIP IT        0       00227
         B     AAA050             NOJCL SPECIFIED, GO TO IT      1      00228
         OI    PSCBATR1,PSCBJCL   ALLOW JCL                      2      00229
         B     EXIT050                                                  00230
         SPACE                                                          00231
AAA050   EQU   *                                                        00232
         NI    PSCBATR1,X'FF'-PSCBJCL  RESET JCL                        00233
         SPACE                                                          00234
EXIT050  EQU   *                                                        00235
         B     ENTRY060                                                 00236
         EJECT                                                          00237
*---------------------------------------------------------------------* 00238
*                                                                       00239
*        PROCESS MOUNT OPERANDS                                         00240
*                                                                       00241
*---------------------------------------------------------------------* 00242
         SPACE                                                          00243
ENTRY060 DS    0H                                                       00244
         LH    R15,MOUNT          GET NUMBER OF SPECIFIED KEYWORD       00245
         SLL   R15,2              MULTIPLY BY FOUR                      00246
         B     *+4(R15)           TO PROCESSING ROUTINE                 00247
         B     EXIT060            NOT SPECIFIED, SKIP IT          0     00248
         B     AAA060             NOMOUNT SPECIFIED, GO TO IT        1  00249
         OI    PSCBATR1,PSCBVMNT  ALLOW MOUNT                     2     00250
         B     EXIT060                                                  00251
         SPACE                                                          00252
AAA060   EQU   *                                                        00253
         NI    PSCBATR1,X'FF'-PSCBVMNT  RESET MOUNT                     00254
         SPACE                                                          00255
EXIT060  DS    0H                                                       00256
         B     ENTRY070                                                 00257
         EJECT                                                          00258
*---------------------------------------------------------------------* 00259
*                                                                       00260
*        PROCESS PASSWORD OPERANDS                                      00261
*                                                                       00262
*---------------------------------------------------------------------* 00263
         SPACE                                                          00264
ENTRY070 DS    0H                                                       00265
         L     R1,16          GET CVT POINTER                           00266
         L     R1,0(R1)       OLD/NEW POINTER                           00267
         L     R1,4(R1)       OUR TCB POINTER                           00268
         L     R2,180(R1)     JSCB POINTER                              00269
         USING IEZJSCB,R2                                               00270
         OI    JSCBSWT1,JSCBPASS  NO PASSWORD CHECKING                  00271
         LH    R15,PASS           GET NUMBER OF SPECIFIED KEYWORD       00272
         SLL   R15,2              MULTIPLY BY FOUR                      00273
         B     *+4(R15)           TO PROCESSING ROUTINE                 00274
         B     EXIT070            NOT SPECIFIED, SKIP IT          0     00275
         B     AAA070             NOPASS SPECIFIED, GO TO IT      1     00276
         NI    JSCBSWT1,X'FF'-JSCBPASS  NO PASSWORD CHECKING      2     00277
         B     EXIT070                                                  00278
         SPACE                                                          00279
AAA070   EQU   *                                                        00280
         OI    JSCBSWT1,JSCBPASS  ALLOW PASSWORD CHECKING               00281
         SPACE                                                          00282
EXIT070  DS    0H                                                       00283
         B     RETURN                                                   00284
         DROP  R2                                                       00285
         EJECT                                                          00286
*---------------------------------------------------------------------* 00287
         SPACE                                                          00288
RETURN   DS    0H                                                       00289
         MODESET KEY=ZERO                                               00290
         AUTH  OFF                GET  RID OF AUTHORIZATION             00291
         L     R15,RC             RESTORE RETURN CODE                   00292
         FINISH RC=(R15)                                                00293
         SPACE 2                                                        00294
*---------------------------------------------------------------------* 00295
BLANKS   DC    81C' '             BLANK FOLLOWED BY 80 BYTE BUFFER      00296
ECB      DC    F'0'               DUMB ECB FOR PARSE                    00297
PPLANSR  DC    F'0'               PARSE ANSWER AREA POINTER             00298
RC       DC    F'0'               RETURN CODE SAVE AREA                 00299
*---------------------------------------------------------------------* 00300
PPLUPT   DC    F'0'               USER PROFILE TABLE POINTER            00301
PPLECT   DC    F'0'               ENVIRONMENT CONTROL TABLE             00302
PPLECB   DC    A(ECB)             ECB POINTER                           00303
PPLPCL   DC    A(IKJPCL)          ADDRESS OF IKJPARM                    00304
PPLANS   DC    A(PPLANSR)         PLACE TO PUT ANSWER                   00305
PPLCBUF  DC    F'0'               POINTER TO COMMAND BUFFER             00306
*---------------------------------------------------------------------* 00307
         SPACE 2                                                        00308
         LTORG                                                          00309
         EJECT                                                          00310
IKJPCL   IKJPARM                                                        00311
USERID   IKJKEYWD                                                       00312
         IKJNAME 'RESET'                                                00313
         IKJNAME 'ID',SUBFLD=IDSUBF                                     00314
         IKJNAME 'LISTID'                                               00315
         IKJNAME 'LID'                                                  00316
UNIT     IKJKEYWD                                                       00317
         IKJNAME 'NOUNIT'                                               00318
         IKJNAME 'UNIT',SUBFLD=UNITSUBF                                 00319
         IKJNAME 'LISTUNIT'                                             00320
         IKJNAME 'LUNIT'                                                00321
DEST     IKJKEYWD                                                       00322
         IKJNAME 'NODEST'                                               00323
         IKJNAME 'DEST',SUBFLD=DESTSUBF                                 00324
         IKJNAME 'LISTDEST'                                             00325
         IKJNAME 'LDEST'                                                00326
JCL      IKJKEYWD                                                       00327
         IKJNAME 'NOJCL'                                                00328
         IKJNAME 'JCL'                                                  00329
MOUNT    IKJKEYWD                                                       00330
         IKJNAME 'NOMOUNT'                                              00331
         IKJNAME 'MOUNT'                                                00332
PASS     IKJKEYWD                                                       00333
         IKJNAME 'NOPASS'                                               00334
         IKJNAME 'PASS'                                                 00335
IDSUBF   IKJSUBF                                                        00336
UID      IKJPOSIT  USERID,PROMPT='NEW USER-ID TO BE USED'               00337
UNITSUBF IKJSUBF                                                        00338
UNITN    IKJIDENT  'UNITNAME',MAXLNTH=8,FIRST=ALPHANUM,OTHER=ALPHANUM, X00339
               PROMPT='UNIT NAME FOR DEFAULT DEVICE ALLOCATIONS'        00340
DESTSUBF IKJSUBF                                                        00341
DESTN    IKJIDENT  'DEST',MAXLNTH=8,FIRST=ALPHA,OTHER=ALPHANUM,        X00342
               PROMPT='DESTID FOR DEFAULT SYSOUT/JOB ROUTING'           00343
         IKJENDP                                                        00344
         SPACE                                                          00345
         PRINT OFF                                                      00346
         IKJCPPL                                                        00347
         IKJPSCB                                                        00348
         IEZJSCB                                                        00349
         SPACE                                                          00350
         END   UADS                                                     00351
